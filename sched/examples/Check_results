#!/bin/csh -f

#  Compare results of a run of sched with "standard" results somewhere.
#  Inputs  $1 is the expcode of the new results.
#          $2 is the station to compare.
#          $3 is the directory for the "standard" results.
#          $4 is the base name of the .key file.
#          sed used to remove directory name differences.
#           Warning - Check_sed must not have blanks at end of lines.

#   The -f on the first line tries to prevent $SCHED from being reset.

#  Compare the .key files.

if( $SCHED !~ */ ) then
  setenv SCHED_SUB $SCHED_STD
else
  setenv SCHED_SUB "$SCHED_STD"/
endif

echo ' '
echo '-------  diffs - local file vs file in ' $3 '-----------'
echo '----------------------' $4 '-------------------------'
sed -e s:"$SCHED":"$SCHED_SUB":g $4 | \
  sed -f Check_sed | diff - $3/$4

#  Check the .runlog file. 

echo '----------------------' $1.runlog '-------------------------'
sed -e s:"$SCHED":"$SCHED_SUB":g $1.runlog | \
  sed -f Check_sed | diff - $3/$1.runlog

#   Check crd file of the requested station, if present.

echo '----------------------' $1.sum '-------------------------'
if( -e $1.sum ) then
  sed -e s:"$SCHED":"$SCHED_SUB":g $1.sum | \
     sed -f Check_sed | diff - $3/$1.sum
else
  echo '*************  SCHED did not run. ***************'
  echo 'This might be ok if the example tried to use an unsupported'
  echo 'feature for this installation, like satellite tracking.'
endif

#   Check crd file of the requested station, if present.

if( -e $1crd.$2 ) then
  echo '----------------------' $1crd.$2 '-----------------------'
  sed -f Check_sed $1crd.$2 | diff - $3/$1crd.$2
endif

#   Check sch file of the requested station, if present.

if( -e $1sch.$2 ) then
echo '----------------------' $1sch.$2 '-----------------------'
  sed -e s:"$SCHED":"$SCHED_SUB":g $1sch.$2 | \
    sed -f Check_sed | diff - $3/$1sch.$2
endif

#  Check the flag file, if present.

if( -e $1.flag ) then
echo '----------------------' $1.flag '-----------------------'
   diff $1.flag $3/$1.flag
endif

#  Check any VLA files that might be present.

if( -e $1obs.y ) then
  echo '----------------------' $1obs.y ' -----------------------'
  sed -f Check_sed $1obs.y | diff - $3/$1obs.y
endif
if( -e $1obs.y && $2 != "y" ) then
  echo '----------------------' $1crd.y ' -----------------------'
  sed -f Check_sed $1crd.y | diff - $3/$1crd.y
endif

#  Check any Green Bank files that might be present.

if( -e $1obs.gb ) then
  echo '----------------------' $1obs.gb ' -----------------------'
  sed -f Check_sed $1obs.gb | diff - $3/$1obs.gb
  echo '----------------------' $1crd.gb ' -----------------------'
  sed -f Check_sed $1crd.gb | diff - $3/$1crd.gb
endif

#  Check any Snap files that might be present (from the mark2 file)

if( -e $1obs.mc ) then
  echo '----------------------' $1obs.mc ' -----------------------'
  sed -f Check_sed $1obs.mc | diff - $3/$1obs.mc
endif

#  Check a VEX file if one is present.

if( -e $1.vex ) then
  echo '----------------------' $1.vex ' ------------------------'
  sed -f Check_sed $1.vex | diff - $3/$1.vex
endif

echo '----------------------------------------------------------'
echo ' '

#  Check an OMS file if one is present.

if( -e $1.oms ) then
  echo '----------------------' $1.oms ' ------------------------'
  sed -f Check_sed $1.oms | diff - $3/$1.oms
endif

echo '----------------------------------------------------------'
echo ' '

