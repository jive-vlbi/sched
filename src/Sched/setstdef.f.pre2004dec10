      SUBROUTINE SETSTDEF( KS )
C
C     Routine for SCHED called by SETEXPND that sets the stations in
C     a setup file prior to the expansion into single station groups.
C     If station = ' ', this routine looks for all stations that
C     use this setup and adds them to the list.  If stations are
C     specified, it eliminates the ones that are not in the schedule
C     (this helps avoid having excessive groups for, for example,
C     VLBA only projects that use lots of setups).
C
C     Note that if the user takes the station='default' option, 
C     this routine will just treat that as a valid station and that
C     setup group will apply to all stations.  This is a bad idea
C     if there are any variations of parameters between stations.
C
C     At the time this routine is run, the automatic pointing scan
C     insertion has not happened.  Therefore we will need to assume
C     that any setups that might be invoked by that process actually
C     are used.
C
      INCLUDE      'sched.inc'
      INCLUDE      'schset.inc'
      INCLUDE      'schpeak.inc'
C
      INTEGER     KS, JS, ISCN, ISTA, KSTA, JSTA, NOUT
      LOGICAL     GOTVLBA, GOTNEW, SETVLBA, NEEDVLBA
      CHARACTER   NEWSTA(2*MANT)*8
C ----------------------------------------------------------------------
      IF( DEBUG ) CALL WLOG( 0, 'SETSTDEF: Starting.' )
C
      DO NOUT = 1, MANT
         NEWSTA(NOUT) = ' '
      END DO
C
      IF( SETSTA(1,KS) .EQ. ' ' ) THEN
C
C        No station was specified.  Gather all that used this setup
C        in the schedule, including any that might use it in peaking
C        scans that have not yet been inserted.
C
C        Loop over stations in the schedule, then scans (for simple 
C        schedules, this will be quick as we won't get past the 
C        first scan or first few scans.
C
         NOUT = 0
         DO ISTA = 1, NSTA
            GOTNEW = .FALSE.
            DO ISCN = 1, NSCANS
               IF( STASCN(ISCN,ISTA) .AND. 
     1             SETNUM(ISCN) .EQ. ISETNUM(KS) ) THEN
                  NOUT = NOUT + 1
                  NEWSTA(NOUT) = STANAME(ISTA)
                  GO TO 100
               END IF
            END DO
  100       CONTINUE
         END DO
C
C        Now add any stations that might get added by the automatic
C        pointing scan insertions.  Utilize PKGROUP that was set up
C        in RDPEAK to simplify this sort of thing.
C
         IF( DOPOINT .AND. NPKGRP .GT. 0 ) THEN
            DO ISTA = 1, NSTA
               IF( PKGROUP(ISTA) .NE. 0 ) THEN
                  IF( PKLSET(PKGROUP(ISTA)) .EQ. ISETNUM(KS) ) THEN
                     NOUT = NOUT + 1
                     NEWSTA(NOUT) = STANAME(ISTA)
                  END IF
               END IF
            END DO
         END IF
C
C
C        We now have a complete list of stations using this setup file.
C 
C        Go through and eliminate any that are covered explicitly under
C        other groups in the setup file.  Also, while at it, 
C        detect whether any default type names (eg VLBA) were
C        set in other groups and set the flag that determines whether
C        to use VLBA in this setup.
C
         SETVLBA = .FALSE.
         IF( NOUT .NE. 0 ) THEN
C
C           Determine if there are any VLBA stations using the file.
C
            DO ISTA = 1, NOUT
               IF( NEWSTA(ISTA)(1:4) .EQ. 'VLBA' ) SETVLBA = .TRUE.
            END DO
C
C           Look at the other groups in this setup file and eliminate
C           stations covered there.
C
            DO JS = 1, NSET
               IF( JS .NE. KS .AND. ISETNUM(JS) .EQ. ISETNUM(KS) ) THEN
C
C                 Loop over stations in the other group.
C
                  DO KSTA = 1, MANT
C
C                    If 'VLBA' was specified there, prevent 
C                    setting it here in the current group.  Also
C                    eliminate any VLBA stations from the current group
C                    since they will be covered in the other group.
C
                     IF( SETSTA(KSTA,JS) .EQ. 'VLBA' ) THEN
                        SETVLBA = .FALSE.
                        DO ISTA = 1, NOUT
                           IF( NEWSTA(ISTA)(1:4) .EQ. 'VLBA' ) THEN
                              NEWSTA(ISTA) = ' '
                           END IF
                        END DO
                     END IF
C
C                    Eliminate any stations specified in the other 
C                    group.
C
                     DO ISTA = 1, NOUT
                        IF( NEWSTA(ISTA) .EQ. SETSTA(KSTA,JS) ) THEN
                           NEWSTA(ISTA) = ' '
                        END IF
                     END DO
C
                  END DO
               END IF
            END DO
         END IF
C
      ELSE
C
C        Some station was specified.  Pay attention to the that spec.
C
C        Don't substitute VLBA for VLBA stations if it was not
C        set explicitly.  This allows specific VLBA stations
C        to have special setups.
C
         SETVLBA = .FALSE.
C
C        Stations were specified.  Eliminate any that were not used.
C        Loop over stations in the setup file, identify which station
C        in the schedule it is, if any, then see if that station is
C        in a scan that requests the setup file.
C
         NOUT = 0
         DO KSTA = 1, MANT
C
C           See if this station was even in the schedule.
C
            DO ISTA = 1, NSTA
               IF( STANAME(ISTA) .EQ. SETSTA(KSTA,KS) .OR.
     1             ( STANAME(ISTA)(1:4) .EQ. 'VLBA' .AND.
     2               SETSTA(KSTA,KS) .EQ. 'VLBA' ) ) THEN
C
C                 If it's in the schedule, see if it is in a scan
C                 that requests this setup.
C
                  DO ISCN = 1, NSCANS
                     IF( STASCN(ISCN,ISTA) .AND. 
     1                   SETNUM(ISCN) .EQ. ISETNUM(KS) ) THEN
                        NOUT = NOUT + 1
                        NEWSTA(NOUT) = SETSTA(KSTA,KS) 
                        GO TO 400
                     END IF
                  END DO
C
C                 If the station was not found in the schedule, see
C                 if it might get inserted for a peaking scan.
C 
                  IF( PKGROUP(ISTA) .NE. 0 ) THEN
                     IF( PKLSET(PKGROUP(ISTA)) .EQ. ISETNUM(KS) ) THEN
                        NOUT = NOUT + 1
                        NEWSTA(NOUT) = SETSTA(KSTA,KS)
                        GO TO 400
                     END IF
                  END IF
               END IF
            END DO
  400       CONTINUE
         END DO
C
      END IF
C
C     We now have NOUT stations specified in NEWSTA, but some may be 
C     blank, having been eliminated because of duplication.
C
C     If there were VLBA stations in the schedule, but they were
C     not explicitly specified in the setup file, substitute "VLBA" 
C     for them.  The resulting duplicates will be removed below.
C
      IF( SETVLBA ) THEN
         DO ISTA = 1, NOUT
            IF( NEWSTA(ISTA)(1:4) .EQ. 'VLBA' ) THEN
               NEWSTA(ISTA) = 'VLBA    '
            END IF
         END DO
      END IF
C
C     Eliminate duplicates.
C     Includes eliminating multiple "VLBA"s
C
      DO KSTA = 1, NOUT - 1
         DO ISTA = KSTA + 1, NOUT
            IF( NEWSTA(KSTA) .EQ. NEWSTA(ISTA) ) THEN
               NEWSTA(ISTA) = ' '
            END IF
         END DO
      END DO
C
C     Eliminate a "VLBA" station if all used VLBA stations in the
C     schedule were explicitly set in one of the setup groups
C     associated with this file.
C
      DO KSTA = 1, NOUT
         IF( NEWSTA(KSTA) .EQ. 'VLBA' ) THEN
C
C           Look for VLBA stations in the schedule station list.
C
            NEEDVLBA = .FALSE.
            DO ISTA = 1, NSTA
               IF( STANAME(ISTA)(1:4) .EQ. 'VLBA' ) THEN
C
C                 For each found, see if it is explicitly specified
C                 somewhere else for this setup file.  If any is
C                 found that is not, jump out and keep the "VLBA".
C                 If all are found, drop the "VLBA".
C
                  GOTVLBA = .FALSE.
                  DO JS = 1, NSET
                     IF( ISETNUM(JS) .EQ. ISETNUM(KS) ) THEN
                        DO JSTA = 1, MANT
                           IF( SETSTA(JSTA,JS) .EQ. STANAME(ISTA) ) THEN
                              GOTVLBA = .TRUE.
                           END IF
                        END DO
                     END IF
                  END DO
                  IF( .NOT. GOTVLBA ) NEEDVLBA = .TRUE.
               END IF
            END DO
C
C           Now take out the "VLBA" if it is not needed.
C
            IF( .NOT. NEEDVLBA ) THEN
               NEWSTA(KSTA) = ' '
            END IF
         END IF
      END DO
C
C     Now replace the station list with the new one.  Note that
C     all will be blank if no used stations were found.  NEWSTA
C     may have had stations eliminated above so not all elements
C     should be used.  First reinitialize the SETSTA.
C
      DO KSTA = 1, MANT
         SETSTA(KSTA,KS) = ' '
      END DO
C
      ISTA = 0
      DO KSTA = 1, MANT
         IF( NEWSTA(KSTA) .NE. ' ' .AND. ISTA .LT. MANT ) THEN
            ISTA = ISTA + 1
            SETSTA(ISTA,KS) = NEWSTA(KSTA)
         ELSE IF( NEWSTA(KSTA) .NE. ' ' ) THEN
            WRITE( MSGTXT, '( A, A )' )
     1         'SETSTDEF: Trying to expand to too ',
     2         'many stations in setup file.'
            CALL WLOG( 1, MSGTXT )
            CALL WLOG( 1, SETNAME(KS) )
            CALL ERRLOG( 'Report problem and try multiple groups '//
     1           'in setup file.' )
         END IF
      END DO
      NOUT = ISTA
C
      RETURN
      END


