C$Procedure      DROTAT ( Derivative of a rotation matrix )
 
      SUBROUTINE DROTAT ( ANGLE, IAXIS, DMOUT )
 
C$ Abstract
C
C      Calculate the derivative with respect to the angle of rotation
C      of a 3x3 coordinate system rotation matrix generated by a
C      rotation of a specified angle about a specified axis.
C
C$ Disclaimer
C
C     THIS SOFTWARE AND ANY RELATED MATERIALS WERE CREATED BY THE
C     CALIFORNIA INSTITUTE OF TECHNOLOGY (CALTECH) UNDER A U.S.
C     GOVERNMENT CONTRACT WITH THE NATIONAL AERONAUTICS AND SPACE
C     ADMINISTRATION (NASA). THE SOFTWARE IS TECHNOLOGY AND SOFTWARE
C     PUBLICLY AVAILABLE UNDER U.S. EXPORT LAWS AND IS PROVIDED "AS-IS"
C     TO THE RECIPIENT WITHOUT WARRANTY OF ANY KIND, INCLUDING ANY
C     WARRANTIES OF PERFORMANCE OR MERCHANTABILITY OR FITNESS FOR A
C     PARTICULAR USE OR PURPOSE (AS SET FORTH IN UNITED STATES UCC
C     SECTIONS 2312-2313) OR FOR ANY PURPOSE WHATSOEVER, FOR THE
C     SOFTWARE AND RELATED MATERIALS, HOWEVER USED.
C
C     IN NO EVENT SHALL CALTECH, ITS JET PROPULSION LABORATORY, OR NASA
C     BE LIABLE FOR ANY DAMAGES AND/OR COSTS, INCLUDING, BUT NOT
C     LIMITED TO, INCIDENTAL OR CONSEQUENTIAL DAMAGES OF ANY KIND,
C     INCLUDING ECONOMIC DAMAGE OR INJURY TO PROPERTY AND LOST PROFITS,
C     REGARDLESS OF WHETHER CALTECH, JPL, OR NASA BE ADVISED, HAVE
C     REASON TO KNOW, OR, IN FACT, SHALL KNOW OF THE POSSIBILITY.
C
C     RECIPIENT BEARS ALL RISK RELATING TO QUALITY AND PERFORMANCE OF
C     THE SOFTWARE AND ANY RELATED MATERIALS, AND AGREES TO INDEMNIFY
C     CALTECH AND NASA FOR ALL THIRD-PARTY CLAIMS RESULTING FROM THE
C     ACTIONS OF RECIPIENT IN THE USE OF THE SOFTWARE.
C
C$ Required_Reading
C
C     ROTATION
C
C$ Keywords
C
C      MATRIX
C      ROTATION
C      DERIVATIVE
C
C$ Declarations
 
      DOUBLE PRECISION   ANGLE
      INTEGER            IAXIS
      DOUBLE PRECISION   DMOUT  ( 3,3 )
 
C$ Brief_I/O
C
C      VARIABLE  I/O              DESCRIPTION
C      --------  ---  --------------------------------------------------
C      ANGLE      I   Angle of rotation (radians).
C      IAXIS      I   Coordinate axis number (X=1, Y=2, Z=3).
C      DMOUT      O   Derivative of rotation matrix [ANGLE] w.r.t. angle
C                                                          IAXIS
C$ Detailed_Input
C
C      ANGLE   The angle given in radians, through which the rotation
C              is performed.
C
C      IAXIS   The coordinate axis number of the rotation.  The X, Y,
C              and Z axes have indices 1, 2 and 3 respectively.
C
C              Together ANGLE and IAXIS define the coordinate system
C              rotation [ANGLE]     .
C                              IAXIS
C
C$ Detailed_Output
C
C      DMOUT    Derivative of rotation matrix with respect to the
C               angle of rotation. That is, DMOUT is the derivative
C               with respect to ANGLE of the matrix [ANGLE]     .
C                                                          IAXIS
C
C               (The rotation matrix being  differentiated describes
C               the rotation of the COORDINATE system through ANGLE
C               radians about the axis whose index is IAXIS.)
C
C$ Parameters
C
C      None.
C
C$ Particulars
C
C      A coordinate system rotation by ANGLE radians rotation about
C      the first, i.e. x-axis, [ANGLE]  is described by
C                                     1
C
C            |  1        0          0      |
C            |  0   cos(ANGLE) sin(ANGLE)  |
C            |  0  -sin(ANGLE) cos(ANGLE)  |
C
C      A coordinate system rotation by ANGLE radians rotation about the
C      second, i.e. y-axis, [ANGLE]  is described by
C                                  2
C
C            |  cos(ANGLE)  0  -sin(ANGLE)  |
C            |      0       1        0      |
C            |  sin(ANGLE)  0   cos(ANGLE)  |
C
C      A coordinate system rotation by ANGLE radians rotation about
C      the third, i.e. z-axis, [ANGLE]  is described by
C                          3
C
C            |  cos(ANGLE) sin(ANGLE)   0   |
C            | -sin(ANGLE) cos(ANGLE)   0   |
C            |       0          0       1   |
C
C      The derivatives of these matrices are:
C
C      about the x-axis
C
C            |  0        0           0      |
C            |  0  -sin(ANGLE)  cos(ANGLE)  |
C            |  0  -cos(ANGLE) -sin(ANGLE)  |
C
C      about the y-axis
C
C            | -sin(ANGLE)  0  -cos(ANGLE)  |
C            |      0       0        0      |
C            |  cos(ANGLE)  0  -sin(ANGLE)  |
C
C      about the z-axis
C
C            | -sin(ANGLE)  cos(ANGLE)   0   |
C            | -cos(ANGLE) -sin(ANGLE)   0   |
C            |       0           0       0   |
C
C$ Examples
C
C      If ROTATE is called from a FORTRAN program as follows:
C
C            CALL DROTAT (PI()/4, 3, DMOUT)
C
C      then DMOUT will be
C
C            |-SQRT(2)/2   SQRT(2)/2   0  |
C            |-SQRT(2)/2  -SQRT(2)/2   0  |
C            |     0           0       0  |
C
C$ Restrictions
C
C      None.
C
C$ Exceptions
C
C     1) If the axis index is not in the range 1 to 3 the error
C        'SPICE(BADAXIS)' will be signalled.
C
C$ Files
C
C      None.
C
C$ Author_and_Institution
C
C      W.L. Taber (JPL)
C
C$ Literature_References
C
C      None.
C
C$ Version
C
C-    SPICELIB Version 1.0.1, 10-MAR-1992 (WLT)
C
C        Comment section for permuted index source lines was added
C        following the header.
C
C-    SPICELIB Version 1.0.0, 5-NOV-1990 (WLT)
C
C-&
 
C$ Index_Entries
C
C     derivative of a rotation matrix
C
C-&
 
 
      DOUBLE PRECISION      S
      DOUBLE PRECISION      C
 
      INTEGER               I1
      INTEGER               I2
      INTEGER               I3
      INTEGER               INDEXS ( 5 )
      SAVE                  INDEXS
 
      DATA                  INDEXS  / 1, 2, 3, 1, 2 /
 
 
 
C
C     First make sure the input axis is reasonable.
C
      IF (      ( IAXIS .GT. 3 )
     .     .OR. ( IAXIS .LT. 1 ) ) THEN
 
         CALL CHKIN  ( 'DROTAT' )
         CALL SETMSG ( 'The input axis is out of range.  Its value '  //
     .                 'is #.')
         CALL ERRINT ( '#', IAXIS       )
         CALL SIGERR ( 'SPICE(BADAXIS)' )
         CALL CHKOUT ( 'DROTAT'         )
         RETURN
 
      END IF
 
C
C     Get the sine and cosine of ANGLE
C
      S  = DSIN(ANGLE)
      C  = DCOS(ANGLE)
 
C
C     Get indices for axes. The first index is for the axis of rotation.
C     The next two axes follow in right hand order (XYZ).
C
      I1 = INDEXS( IAXIS     )
      I2 = INDEXS( IAXIS + 1 )
      I3 = INDEXS( IAXIS + 2 )
 
C
C  Construct the rotation matrix
C
      DMOUT(I1,I1) =  0.D0
      DMOUT(I2,I1) =  0.D0
      DMOUT(I3,I1) =  0.D0
      DMOUT(I1,I2) =  0.D0
      DMOUT(I2,I2) = -S
      DMOUT(I3,I2) = -C
      DMOUT(I1,I3) =  0.D0
      DMOUT(I2,I3) =  C
      DMOUT(I3,I3) = -S
C
      RETURN
      END
 
 
 
